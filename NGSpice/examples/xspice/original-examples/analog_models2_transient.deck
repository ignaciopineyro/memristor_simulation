Code Model Test - Transient: int, d_dt, s_xfer, core, lcouple
*
*
*** analysis type ***
.tran .1s 10s
*
*** input sources ***
*
v1 1 0 DC PWL(0 0 10 10) 
*
v2 2 0 DC PWL(0 0   0.1 0.1   0.2 0.9   0.3 1.0   10 1.0) 
*
*
*
*** integrator block ***
a1 1 10 int1 
.model int1 int (in_offset=0.0 gain=2.0 out_lower_limit=-1.0e6
+                out_upper_limit=1.0e6 limit_range=1.0e-6 out_ic=0.0)
*
*
*** differentiator block ***
a2 1 20 d_dt1
.model d_dt1 d_dt (out_offset=0.0 gain=1.0 out_lower_limit=-1.0e6
+                  out_upper_limit=1.0e6 limit_range=1.0e-6)
*
*
*** s_xfer block ***
a3 2 30 filter1
.model filter1 s_xfer (in_offset=0.0 gain=1.0 num_coeff=[1.0]
+                      den_coeff=[1.0 1.414214 1.0] int_ic=[0.0 0.0] 
+                      denormalized_freq=1.0)
*                                 
*
*
*** magnetic core & inductive coupling ***
a4 40 45 core1
.model core1 core (H_array=[-2.0 -1.0 1.0 2.0] 
+                  B_array=[-2.0 -1.0 1.0 2.0]
+                  area=1.0 length=1.0 input_domain=1.0e-6
+                  fraction=TRUE mode=1 
+                  in_low=-1.0 in_high=1.0 hyst=0.5 
+                  out_lower_limit=-1.0 out_upper_limit=1.0)
*
*
r5 1 50 100.0
a5 (50 0) (40 45) inductor1
.model inductor1 lcouple (num_turns=10)
*
*
*** resistors to ground ***
r1 1 0 1k
r2 2 0 1k
*
r10 10 0 1k
r20 20 0 1k
r30 30 0 1k
r40 40 0 1k
r50 50 0 1k
*
*
.end






